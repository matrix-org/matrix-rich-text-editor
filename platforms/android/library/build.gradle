plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'

    // Rust
    id 'org.mozilla.rust-android-gradle.rust-android'
    id("com.dicedmelon.gradle.jacoco-android") version "0.1.5"
}

if (project.hasProperty("coverage")) {
    apply plugin: 'jacoco'
}

apply plugin: "com.vanniktech.maven.publish"

cargo {
    module = "../../../bindings/wysiwyg-ffi"    // The directory which contains Cargo.toml
    libname = "uniffi-wysiwyg-composer"         // The Cargo.toml's [package] name.
    targets = ["arm", "x86", "x86_64", "arm64"]
    targetIncludes = ["libuniffi_wysiwyg_composer.so"]
    targetDirectory = '../../../target'
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        // Disables animations during instrumented tests you run from the command line…
        // This property does not affect tests that you run using Android Studio.”
        animationsDisabled = true

        unitTests {
            includeAndroidResources = true
        }
    }

    testCoverage {
        jacocoVersion = "0.8.8"
    }

    ndkVersion = "25.2.9519653"
}

dependencies {

    implementation "net.java.dev.jna:jna:5.7.0@aar"

    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4")

    implementation 'com.jakewharton.timber:timber:5.0.1'

    // XML Parsing
    api 'org.ccil.cowan.tagsoup:tagsoup:1.2'

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.8'
    testImplementation 'io.mockk:mockk:1.12.5'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.4.0'
    androidTestImplementation 'io.mockk:mockk-android:1.12.5'
}

android.libraryVariants.all { variant ->
    def generateBindingTasks = tasks.register("generate${variant.name.capitalize()}UniFFIBindings", Exec) {
        workingDir "${project.projectDir}"
        commandLine 'cargo', 'uniffi-bindgen', 'generate', '../../../bindings/wysiwyg-ffi/src/wysiwyg_composer.udl', '--language', 'kotlin', '--out-dir', "${buildDir}/generated/source/uniffi/${variant.name}/java"
        dependsOn("cargoBuild")
    }
    tasks.preBuild.dependsOn(generateBindingTasks)
    def sourceSet = variant.sourceSets.find { it.name == variant.name }
    sourceSet.java.srcDir new File(buildDir, "generated/source/uniffi/${variant.name}/java")
}

afterEvaluate {
    def taskName = getGradle().getStartParameter().getTaskRequests().toString()
    def isReleaseBuild = taskName.containsIgnoreCase("release") || taskName.containsIgnoreCase("publish")
    if (isReleaseBuild) {
        cargo.profile = "release"
    } else {
        cargo.profile = "debug"
        cargo.features {
            defaultAnd("assert-invariants")
        }
    }
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

